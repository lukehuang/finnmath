/*
 * BSD 2-Clause License
 *
 * Copyright (c) 2017, Lars Tennstedt
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 * list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 * this list of conditions and the following disclaimer in the documentation
 * and/or other materials provided with the distribution.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
 * CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
 * OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

import net.ltgt.gradle.errorprone.ErrorProneToolChain

buildscript {
    repositories.maven { url 'https://plugins.gradle.org/m2/' }
    
    dependencies { classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.11' }
}

apply plugin: 'java-library'
apply plugin: 'net.ltgt.errorprone-base'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'

description 'A math library for the JVM based on BigInteger and BigDecimal'
group 'finnmath'
version '1-SNAPSHOT'
sourceCompatibility = 1.8

compileJava { toolChain ErrorProneToolChain.create(project) }

repositories { jcenter() }

dependencies {
	api 'com.google.guava:guava:23.0'
	api 'org.apache.commons:commons-lang3:3.6'
	implementation 'org.slf4j:slf4j-api:1.7.25'
	implementation 'org.slf4j:slf4j-simple:1.7.25'
	testCompile 'org.assertj:assertj-core:3.8.0'
	testCompile 'junit:junit:4.12'
}

tasks.withType(Checkstyle) { reports { xml.enabled false } }

tasks.withType(FindBugs) {
	reports { 
		xml.enabled false
		html.enabled true
	}
}

tasks.withType(Pmd) { reports { xml.enabled false} }

javadoc { options.noQualifiers 'all' }

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts { archives javadocJar, sourcesJar }

install.repositories.mavenInstaller.pom.project {
     name 'finnMath'
     description description
     url 'https://github.com/togliu/finnmath'
     inceptionYear '2017'
     licenses {
        license {
            name 'BSD 2-Clause License'
            url 'https://opensource.org/licenses/BSD-2-Clause'
        }
     }
     developers {
        developer {
            id 'togliu'
            name 'Lars Tennstedt'
            roles {
                role 'architect'
                role 'developer'
            }
            timezone 'Europe/Berlin'
        }
     }
     issueManagement {
        system 'GitHub'
        url 'https://github.com/togliu/finnmath/issues'
     }
     scm {
        connection 'scm:git:https://github.com/togliu/finnmath.git'
        tag 'master'
        url 'https://github.com/togliu/finnmath'
    }
}

uploadArchives.repositories { mavenLocal() }

task deleteJavadoc(type: Delete) { delete 'docs' }

task copyJavadoc(type: Copy) {
    from 'build/docs/javadoc'
    into 'docs'
}

eclipse.classpath {
    downloadJavadoc true
    downloadSources true
}

idea.module {
    downloadJavadoc true
    downloadSources true
}
